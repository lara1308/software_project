// Arduino pin assignment
#define PIN_LED  9
#define PIN_TRIG 12   // sonar sensor TRIGGER
#define PIN_ECHO 13   // sonar sensor ECHO

// configurable parameters
#define SND_VEL 346.0     // sound velocity at 24 celsius degree (unit: m/sec)
#define INTERVAL 25      // sampling interval (unit: msec)
#define PULSE_DURATION 10 // ultra-sound Pulse Duration (unit: usec)
#define _DIST_MIN 100.0   // minimum distance to be measured (unit: mm)
#define _DIST_MAX 300.0   // maximum distance to be measured (unit: mm)

#define TIMEOUT ((INTERVAL / 2) * 1000.0) // maximum echo waiting time (unit: usec)
#define SCALE (0.001 * 0.5 * SND_VEL) // coefficent to convert duration to distance
#define CENTER_MM 200.0 //추가한 코드
#define HALF_SPAN 100.0 //추가한 코드

unsigned long last_sampling_time;   // unit: msec

void setup() {
  // initialize GPIO pins
  pinMode(PIN_LED, OUTPUT);
  pinMode(PIN_TRIG, OUTPUT);  // sonar TRIGGER
  pinMode(PIN_ECHO, INPUT);   // sonar ECHO
  digitalWrite(PIN_TRIG, LOW);  // turn-off Sonar 
  
  // initialize serial port
  Serial.begin(57600);
}

void loop() { 
  float distance;

  // wait until next sampling time. // polling
  // millis() returns the number of milliseconds since the program started.
  //    will overflow after 50 days.
  if (millis() < (last_sampling_time + INTERVAL))
    return;

  distance = USS_measure(PIN_TRIG, PIN_ECHO); // read distance

//  if ((distance == 0.0) || (distance > _DIST_MAX)) {
//      distance = _DIST_MAX + 10.0;    // Set Higher Value
//      digitalWrite(PIN_LED, 1);       // LED OFF
//  } else if (distance < _DIST_MIN) {
//      distance = _DIST_MIN - 10.0;    // Set Lower Value
//      digitalWrite(PIN_LED, 1);       // LED OFF
//  } else {    // In desired Range
//      digitalWrite(PIN_LED, 0);       // LED ON      
//  }

// --- PWM 밝기 제어 (active-low): 200mm 밝기 최대(0), 100/300mm 최소(255) ---
int pwm;

if ((distance == 0.0) || (distance > _DIST_MAX) || (distance < _DIST_MIN)) {
  // 타임아웃 또는 범위 밖: 꺼짐
  pwm = 255;
} else {
  // [100,300]로 클램프(수학적 안전성; 경계값에서도 정확히 0/255가 나오게)
  float d = distance;
  if (d < _DIST_MIN) d = _DIST_MIN;
  if (d > _DIST_MAX) d = _DIST_MAX;

  // dev = 중심(200mm)로부터의 정규화 거리 [0..1]
  float dev = (d >= CENTER_MM) ? (d - CENTER_MM) : (CENTER_MM - d);
  dev = dev / HALF_SPAN;                   // 0.0(200mm) ~ 1.0(100/300mm)

  // float → int (0..255), round로 150/250에서 ≈128
  pwm = (int)lroundf(255.0f * dev);
  if (pwm < 0)   pwm = 0;                  // 안전 클램프
  if (pwm > 255) pwm = 255;
}

// analogWrite(pin, 0)=가장 밝음, 255=꺼짐 (active-low)
analogWrite(PIN_LED, pwm);


  // output the distance to the serial port
  Serial.print("Min:");        Serial.print(_DIST_MIN);
  Serial.print(",distance:");  Serial.print(distance);
  Serial.print(",Max:");       Serial.print(_DIST_MAX);
  Serial.println("");
  
  // do something here
  delay(50); // Assume that it takes 50ms to do something.
  
  // update last sampling time
  last_sampling_time += INTERVAL;
}

// get a distance reading from USS. return value is in millimeter.
float USS_measure(int TRIG, int ECHO)
{
  digitalWrite(TRIG, HIGH);
  delayMicroseconds(PULSE_DURATION);
  digitalWrite(TRIG, LOW);
  
  return pulseIn(ECHO, HIGH, TIMEOUT) * SCALE; // unit: mm

  // Pulse duration to distance conversion example (target distance = 17.3m)
  // - pulseIn(ECHO, HIGH, timeout) returns microseconds (음파의 왕복 시간)
  // - 편도 거리 = (pulseIn() / 1,000,000) * SND_VEL / 2 (미터 단위)
  //   mm 단위로 하려면 * 1,000이 필요 ==>  SCALE = 0.001 * 0.5 * SND_VEL
  //
  // - 예, pusseIn()이 100,000 이면 (= 0.1초, 왕복 거리 34.6m)
  //        = 100,000 micro*sec * 0.001 milli/micro * 0.5 * 346 meter/sec
  //        = 100,000 * 0.001 * 0.5 * 346
  //        = 17,300 mm  ==> 17.3m
}
